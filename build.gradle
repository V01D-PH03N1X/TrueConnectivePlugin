plugins {
    id 'java'
    id "com.diffplug.spotless" version "6.25.0"
    id "io.github.goooler.shadow" version "8.1.8"
}

group project.property("group")
version project.property("version")

dependencies {
    annotationProcessor "org.projectlombok:lombok:1.18.32"
    implementation 'org.projectlombok:lombok:1.18.32'
    implementation "dev.triumphteam:triumph-gui:3.1.10"
    implementation 'com.github.jwdeveloper.TikTok-Live-Java:Client:1.8.11-Release'
    compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

spotless {
    java {
        target fileTree("./src/main/java/") {
            include "**/*.java"
            exclude "**/build/**", "**/build-*/**"
        }
        toggleOffOn()
        palantirJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        importOrder()
        formatAnnotations()

        licenseHeaderFile(rootProject.file("LICENSE_HEADER"))
    }
}

shadowJar {
    relocate("dev.triumphteam.gui", "me.mydark.trueconnectiveplugin.gui")
}

tasks.register('buildWithSpotlessAndShadowJar') {
    dependsOn 'spotlessApply'
    finalizedBy 'shadowJar'
}